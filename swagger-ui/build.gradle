/**
 * Makes a webjar from the swagger-ui source on github
 *
 * Other projects can include with: compile project(':swagger-ui')
 *
 */

buildscript {
   dependencies {
      classpath 'de.undercouch:gradle-download-task:1.0'
   }
}

apply plugin: 'download-task'
apply plugin: 'java'

ext {
   downloadUrl = "https://github.com/wordnik/swagger-ui/archive/v${swaggerUiVersion}.zip"
   destinationZip = "$buildDir/zip/${swaggerUiVersion}.zip"
   replacePath = 'window.location.origin + "\\${pageContext.request.contextPath}/api-docs"'
}

task swaggerUiDownload << {
   if (!file(destinationZip)?.exists()) {
      download {
         src downloadUrl
         dest destinationZip
         onlyIfNewer true
      }
   }
}

task unzip(type: Copy) {
   from zipTree(file(destinationZip))
   include "*/dist/**"
   into file("${buildDir}")
}

/**
 * Creates the jsp from index.html
 */
task sdoc(type: Copy){
  from("${buildDir}/${swaggerUiExplodedDir}/index.html")
  into("${buildDir}/${swaggerUiExplodedDir}")

  filter { String line ->
    line.replaceAll("\"http://petstore.swagger.wordnik.com/api/api-docs\"", replacePath)
  }

  rename('index.html', 'sdoc.jsp')
}

jar {
   from("${buildDir}/${swaggerUiExplodedDir}") {
      into "META-INF/resources"
   }
}

compileJava.dependsOn(sdoc)
sdoc.dependsOn(unzip)
unzip.dependsOn(swaggerUiDownload)
